Version 1
SubGoalCombiner SGC_AND
INITSECTION
//REGION Default Dialog 

//END_REGION

//REGION Goblin Fight 
DB_DoNotFaceDialog(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab);
DB_Crime_DoNotStopDialog(Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab);

DB_OneShotPartyTrigger(S_Eli_Ori_VelPos_6f05b45b-7174-4670-96e0-afe1b794aa5c);
DB_AddCharactersInTriggerToDialog(Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab, S_DEN_Vel_FallbackPleaSphere_5bbc1ce8-d0c3-445a-bce0-3347afd43d24, 1, 1, 1);
DB_IgnoreMutingStatussesDialog((DIALOGRESOURCE)Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab);
PROC_TriggerRegisterForPlayers(S_DEN_Vel_FallbackPleaSphere_5bbc1ce8-d0c3-445a-bce0-3347afd43d24);

DB_DEN_Vel_RaidingParty_Participants((CHARACTER)S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f);
DB_DEN_Vel_RaidingParty_Participants(S_DEN_Vel_Melee_99dbcf67-426b-46f5-99ce-b7dc6a902e09);
DB_DEN_Vel_RaidingParty_Participants(S_DEN_Vel_Goblin_Ranger_9349d7a3-b0b7-494c-8479-3b099d6ba55e);

//position flags
DB_GLO_Cinematic_PositionedDialog((DIALOGRESOURCE)Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab,"DEN_Vel_Plea_Pos");
DB_GLO_Cinematic_PositionFlags("DEN_Vel_Plea_Pos",(TRIGGER)S_DEN_Vel_FromDen_dd5fc8aa-d22d-4c9d-8277-71b597b32559,(FLAG)Vel_Start_Event_DenPosition_a1a895fa-083a-4222-aa6f-b352c4c335bf);
DB_GLO_Cinematic_PositionFlags("DEN_Vel_Plea_Pos",(TRIGGER)S_DEN_Vel_FromShip_0e35369d-2d22-4008-b9a7-a19625bf81f4,(FLAG)Vel_Start_Event_ShipPosition_25990d33-8a0f-4cd4-ae73-b9cd9a49ab20);

PROC_DEN_VEL_RaidingParty_SetupPleaExtras();

DB_DEN_VEL_RaidingParty_Goblins((CHARACTER)S_DEN_Vel_Goblin_Ranger_9349d7a3-b0b7-494c-8479-3b099d6ba55e, (TRIGGER)S_DEN_Vel_Goblin_Pos_001_9b671b38-8a83-4694-b7c2-3cc6502c743d, (TRIGGER)S_DEN_Vel_Goblin_AppearPos_001_059b3d15-bc86-435b-a8ab-255538900e2c);
DB_DEN_VEL_RaidingParty_Goblins(S_DEN_Vel_Melee_99dbcf67-426b-46f5-99ce-b7dc6a902e09, S_DEN_Vel_Goblin_Pos_002_6756c9a3-2372-411d-9c06-d5724f2d897d, S_DEN_Vel_Goblin_AppearPos_002_90604789-9d6d-41de-aee5-c16ddc35e797);

DB_GLO_DefeatCounter((CHARACTER)S_DEN_Vel_Goblin_Ranger_9349d7a3-b0b7-494c-8479-3b099d6ba55e, "DEN_RaidingParty_Vel");

PROC_DeclareCounter("DEN_RaidingParty_Vel");

SetCanTrade(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, 0);
DB_DialogBlockAttackButton((DIALOGRESOURCE)Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab);
//END_REGION

KBSECTION
//REGION Setup

IF
DB_DEN_VEL_RaidingParty_Goblins(_Raider, _, _)
THEN
ApplyStatus(_Raider, "AI_NO_LOOK_AT_BATTLE", -1.0, 1, _Raider);

IF
DB_DEN_VEL_RaidingParty_Goblins(_Raider, _, _)
AND
_Raider !=  S_DEN_Vel_Goblin_Ranger_9349d7a3-b0b7-494c-8479-3b099d6ba55e
THEN
DB_GLO_DefeatCounter(_Raider, "DEN_RaidingParty_Vel");

//Custom Music
IF
DB_DEN_VEL_RaidingParty_Goblins(_Raider, _, _)
THEN
DB_Sound_ScriptedCombatMusic(_Raider,"DEN_PleaAtTheGates");

QRY
QRY_CorpseLooting_BlockMakeOwned((CHARACTER)_Raider)
AND
DB_DEN_VEL_RaidingParty_Goblins(_Raider, _, _)
THEN
DB_NOOP(1);

IF
FlagSet((FLAG)DEN_RaidingParty_Quest_VelRaidOver_0e2cdcb0-91f9-496b-8bd5-349910da7275,_,_)
THEN
DB_Dialogs(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, Vel_Main_49fbe3e3-5535-4458-be49-d5490576cdea);
//END_REGION

//REGION Scene Manager and edge case fallbacks

IF
DB_DEN_Vel_RaidingParty_Participants(_Character)
THEN
DB_SceneManager(_Character, "DEN_Vel_Plea");
SetImmortal(_Character, 1);
PROC_SetInvulnerable(_Character, 1);
DB_DEN_Vel_SceneCharacters(_Character);

PROC
PROC_DEN_VEL_RaidingParty_SetupPleaExtras()
THEN
DB_SceneManager(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, "DEN_Vel_Plea");
SetImmortal(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, 1);
PROC_SetInvulnerable(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, 1);

PROC
PROC_SceneInterrupted(_NPC, _Character, "DEN_Vel_Plea", _)
AND
DB_PartyMembers(_Character)
THEN
PROC_DEN_RaidingParty_StartVelPlea(_Character);


PROC
PROC_DEN_RaidingParty_StartVelPlea((CHARACTER)_Player)
AND
DB_Players(_Player)
AND
QRY_StartDialogCustom(Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab, _Player, S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, S_DEN_Vel_Melee_99dbcf67-426b-46f5-99ce-b7dc6a902e09,
				S_DEN_Vel_Goblin_Ranger_9349d7a3-b0b7-494c-8479-3b099d6ba55e, 0, 0, 0, 0)
THEN
PROC_TriggerUnregisterForPlayers(S_Eli_Ori_VelPos_6f05b45b-7174-4670-96e0-afe1b794aa5c);

PROC
PROC_DEN_RaidingParty_StartVelPlea((CHARACTER)_Character)
AND
NOT DB_Players(_Character)
AND
CharacterGetOwner(_Character, _Player)
AND
QRY_StartDialogCustom(Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab, _Player, S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, S_DEN_Vel_Melee_99dbcf67-426b-46f5-99ce-b7dc6a902e09,
				S_DEN_Vel_Goblin_Ranger_9349d7a3-b0b7-494c-8479-3b099d6ba55e, 0, 0, 0, 0)
THEN
PROC_TriggerUnregisterForPlayers(S_Eli_Ori_VelPos_6f05b45b-7174-4670-96e0-afe1b794aa5c);

IF
DialogEnded(Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab, _)
THEN
PROC_SceneOver("DEN_Vel_Plea");

IF
DialogEnded(Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab, _ID)
THEN
PROC_AddCharactersInTriggerToDialog_Clear(Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab, S_DEN_Vel_FallbackPleaSphere_5bbc1ce8-d0c3-445a-bce0-3347afd43d24);

PROC
PROC_SceneOver("DEN_Vel_Plea")
AND
DB_DEN_Vel_SceneCharacters(_Character)
THEN
SetImmortal(_Character, 0);
PROC_SetInvulnerable(_Character, 0);

//END_REGION

//REGION

IF
DialogStarted(Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab, _)
THEN
SetCanTrade(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, 0);

IF
DialogEnded(Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab, _)
THEN
SetCanTrade(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, 1);

//END_REGION

//REGION Plea at Gates
//Start the event at the Gate when the player enters the goblin raid area.
PROC
PROC_OneShotTriggerEntered(_Character, S_Eli_Ori_VelPos_6f05b45b-7174-4670-96e0-afe1b794aa5c)
THEN
PROC_DEN_RaidingParty_StartVelPlea(_Character);

IF
FlagSet(DEN_Vel_Event_UnsheatheForFight_2cb247bb-b7d6-4684-9959-91c47d1c6769, _, _)
AND
DB_DEN_Vel_RaidingParty_Participants(_Character)
AND
NOT DB_DEN_VEL_RaidingParty_Goblins(_Character, _, _)
THEN
LookAtEntity(_Character, S_DEN_Vel_Goblin_Ranger_9349d7a3-b0b7-494c-8479-3b099d6ba55e);
SetWeaponUnsheathed(_Character,1,0);

IF
FlagSet(DEN_Vel_Event_UnsheatheForFight_2cb247bb-b7d6-4684-9959-91c47d1c6769, _, _)
THEN
LookAtEntity(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, S_DEN_Vel_Goblin_Ranger_9349d7a3-b0b7-494c-8479-3b099d6ba55e);
SetWeaponUnsheathed(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f,1,0);

//REGION Add other characters to Plea at Gates

IF
DialogEnded(Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab, _)
THEN
PROC_TriggerUnregisterForPlayers(S_DEN_Vel_FallbackPleaSphere_5bbc1ce8-d0c3-445a-bce0-3347afd43d24);

//END_REGION

//Logic of the fight with the goblins and its setup.
//REGION Goblin Raid Fight

//Set Surprised on non-goblins when event ends
IF
DialogEnded(DIALOGRESOURCEGUID_Vel_Start_13bc5624-fd9e-4787-8084-fe5d07f1e4ab, _)
THEN
PROC_DEN_RaidingParty_VelFightSetup();

PROC
PROC_DEN_RaidingParty_VelFightSetup()
AND
DB_GlobalFlag((FLAG)DEN_Vel_Event_GoblinRaidFight_ff091a73-06c3-4756-9a95-456c126277f1) // flagType: Global
AND
DB_DEN_VEL_RaidingParty_Goblins(_Goblin, _, _)
THEN
FlushOsirisQueue(_Goblin);	//TODO: Remove when code feature is in.
SetFaction(_Goblin, (FACTION)Origin_Vel_Goblins_b9df037b-40c9-4fe5-b9fe-b5ceff63dd61);
SetCombatGroupID(_Goblin, "7becebad-636b-422d-b6ce-0fe2f82de2fe");
PROC_EnterCombat(_Goblin, S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f);

PROC
PROC_DEN_RaidingParty_VelFightSetup()
AND
DB_GlobalFlag((FLAG)DEN_Vel_Event_GoblinRaidFight_ff091a73-06c3-4756-9a95-456c126277f1) // flagType: Global
THEN
SetFaction(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, Origin_Vel_c91013d9-9539-43e0-813a-39c135efad56);
SetRelation(Origin_Vel_c91013d9-9539-43e0-813a-39c135efad56, Origin_Vel_Goblins_b9df037b-40c9-4fe5-b9fe-b5ceff63dd61, 0);
SetRelation(Origin_Vel_Goblins_b9df037b-40c9-4fe5-b9fe-b5ceff63dd61, Origin_Vel_c91013d9-9539-43e0-813a-39c135efad56, 0);
PROC_SetRelationToPlayers((FACTION)Origin_Vel_c91013d9-9539-43e0-813a-39c135efad56, 50);
PROC_SetRelationToPlayers((FACTION)Eilistraeans_a7c0758b-c5f2-420f-97ad-ae881467f405, 50);
PROC_SetRelationToPlayers(Origin_Vel_Goblins_b9df037b-40c9-4fe5-b9fe-b5ceff63dd61, 0);

PROC
PROC_DEN_RaidingParty_VelFightSetup()
AND
NOT DB_GlobalFlag((FLAG)DEN_Vel_Event_GoblinRaidFight_ff091a73-06c3-4756-9a95-456c126277f1) // flagType: Global
THEN
SetFaction(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, Origin_Vel_c91013d9-9539-43e0-813a-39c135efad56);
SetFlag(Vel_Start_Captured_6f878dc2-3e72-45a1-9ed5-0152e0e0c6b4);
PROC_DisappearOutOfSightTo(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f,S_DEN_Vel_DisappearDirection_0da3d81d-cf92-4860-b971-2498bed8ad50,"Walk",0,"");
PROC_Goblins_Follow_Vel();

PROC
PROC_Goblins_Follow_Vel()
AND
DB_DEN_VEL_RaidingParty_Goblins(_Goblin, _, _)
THEN
PROC_Follow(_Goblin,S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f);

IF
WentOnStage(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, 0)
AND
DB_DEN_VEL_RaidingParty_Goblins(_Goblin, _, _)
THEN
PROC_StopFollow(_Goblin);
PROC_DisappearOutOfSightTo(_Goblin, S_DEN_Vel_DisappearDirection_0da3d81d-cf92-4860-b971-2498bed8ad50, "Run", 1, "");
TeleportTo(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f,S_Vel_Goblin_Prison_29edb39f-f3a9-4076-ba6d-28b5a4cdc03d);
SetOnStage(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f,1);

//Throw all party members into combat.
IF
RelationChanged(Origin_Vel_Goblins_b9df037b-40c9-4fe5-b9fe-b5ceff63dd61, Hero_a1542c81-6895-929e-4522-10ce218bb360, 0, _)
THEN
PROC_DEN_Vel_ForceAllIntoCombat();

PROC
PROC_DEN_Vel_ForceAllIntoCombat()
AND
DB_PartyMembers(_Character)
AND
IsInTrigger(_Character, S_DEN_Vel_FallbackPleaSphere_5bbc1ce8-d0c3-445a-bce0-3347afd43d24, 1)
THEN
PROC_EnterCombat(_Character, S_DEN_Vel_Goblin_Ranger_9349d7a3-b0b7-494c-8479-3b099d6ba55e);

IF
FlagSet(DEN_Vel_Event_GoblinRaidFight_ff091a73-06c3-4756-9a95-456c126277f1, _, _)
AND
DB_DEN_Adventurers(_Adventurer)
THEN
FlushOsirisQueue(_Adventurer);

//Save the ID of the goblin raid combat. Used to restrict following events.
IF
EnteredCombat(_Object, _ID)
AND
DB_DEN_VEL_RaidingParty_Goblins((CHARACTER)_Object, _, _)
AND
NOT DB_DEN_Vel_GoblinRaidCombatID(_ID)
THEN
DB_DEN_Vel_GoblinRaidCombatID(_ID);

IF
SwitchedCombat(_Object, _OldID, _NewID)
AND
DB_DEN_Vel_GoblinRaidCombatID(_OldID)
AND
DB_DEN_Vel_RaidingParty_Participants((CHARACTER)_Object)
AND
NOT DB_DEN_Vel_GoblinRaidCombatID(_NewID)
THEN
NOT DB_DEN_Vel_GoblinRaidCombatID(_OldID);
DB_DEN_Vel_GoblinRaidCombatID(_NewID);

IF
CombatEnded(_ID)
AND
DB_DEN_Vel_GoblinRaidCombatID(_ID)
THEN
NOT DB_DEN_Vel_GoblinRaidCombatID(_ID);

PROC
PROC_GLO_DefeatCounter_AllDefeated("DEN_RaidingParty_Vel")
THEN
PROC_DEN_RaidingParty_AssignInspiration();
SetFlag((FLAG)DEN_RaidingParty_Quest_VelRaidOver_0e2cdcb0-91f9-496b-8bd5-349910da7275, NULL_00000000-0000-0000-0000-000000000000, 0); // flagType: Global
PROC_DEN_Vel_GoblinRaidEnded();

PROC
PROC_DEN_Vel_GoblinRaidEnded()
AND
QRY_OnlyOnce("DEN_RaidingParty_VelCleanup")
THEN
SysClear("DB_DEN_VEL_RaidingParty_Goblins", 3);
SysClear("DB_DEN_Vel_RaidingParty_Participants", 1);
SetCombatGroupID(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, "");
PROC_DEN_Vel_SetupAlignmentsAfterGoblinFight();

PROC
PROC_DEN_Vel_SetupAlignmentsAfterGoblinFight()
THEN
PROC_GLO_Origins_SetRecruitmentDialog(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, Vel_Main_49fbe3e3-5535-4458-be49-d5490576cdea);
PROC_Start_Next();

PROC
PROC_Start_Next()
AND
GetClosestPlayer(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, _Player, _Distance)
AND
_Distance < 30.0 
THEN
PROC_CharacterMoveToAndTalk(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, _Player, Vel_Main_49fbe3e3-5535-4458-be49-d5490576cdea, "Vel_Thanks", "Walk", 20.0);

PROC
PROC_Start_Next()
AND
GetClosestPlayer(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, _Player, _Distance)
AND
_Distance > 30.0 
THEN
CharacterMoveTo(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, S_Eli_Ori_VelPos_6f05b45b-7174-4670-96e0-afe1b794aa5c, "Walk", "");

IF
CharacterMoveToAndTalkFailed(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f,_,"Vel_Thanks",_)
THEN
CharacterMoveTo(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f, S_Eli_Ori_VelPos_6f05b45b-7174-4670-96e0-afe1b794aa5c, "Walk", "");

IF
DialogEnded(DIALOGRESOURCEGUID_Vel_Main_49fbe3e3-5535-4458-be49-d5490576cdea, _)
AND
DB_GlobalFlag((FLAG)Vel_Eli_Camp_3f247c16-6ae1-4fc3-bd80-44b6fb723edb)
THEN
PROC_DisappearOutOfSightTo(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f,S_DEN_Vel_DisappearDirection_0da3d81d-cf92-4860-b971-2498bed8ad50,"Walk",1,"");
TeleportTo(S_Vel_Eli_Camp_3ccd1b95-5195-4e2f-b66f-3554639c6365,S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f);
SetOnStage(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f,1);

IF
DialogEnded(DIALOGRESOURCEGUID_Vel_Main_49fbe3e3-5535-4458-be49-d5490576cdea, _)
AND
DB_GlobalFlag((FLAG)Vel_Druid_Den_38c30936-128f-4e83-8b9e-1c1e6bcf5df3)
THEN
PROC_DisappearOutOfSightTo(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f,S_DEN_Vel_DisappearDirection_0da3d81d-cf92-4860-b971-2498bed8ad50,"Walk",1,"");
TeleportTo(S_Vel_Druid_Den_64fd7e01-5e06-415d-b162-6573e9da0196,S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f);
SetOnStage(S_Player_Vel_f83fe475-863b-4325-acf0-106d7bbccb5f,1);

//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "Act1"
